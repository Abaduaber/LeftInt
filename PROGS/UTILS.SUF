;Utils.suf. Утилиты самого разного назначения

;Процедура ReadLn(<VarIndex>).
;Ждет ввода строки пользователем. Строка помещается начиная
;с переменной <VarIndex>
;Поддержка BackSpace. После ввода строки переносит строку.
={ReadLn}=
    ?[3,4]
    $(3)(G)
    $(4)(3$)
  {Inp}
    $(3$)(1$)
    #(3$$)<InpExt> ;Расширенные скан-коды не обрабатывать
    $(1)(3$$)
    $(3)(3$^1+)
    #(3$^1-$^8-)<BackSp>
    #(3$^1-$^13-|)<Inp>
    #<EndInp>
  {BackSp}
    #(4$^3$-)<Inp>
    $(3)(3$^2-)
    $(1)(32)
    $(1)(8)
    #<Inp>
  {InpExt}
    $(4)(1$) 
    #<Inp>
  {EndInp}
    $(3$^1-)(0)
    $(1)(10)
**

;Процедура WriteLn(<VarIndex>)
;Печатает на экране текстовую строку, начинающуюся
;с VarIndex, до первого нуля. После печати ставит
;символы CR и LF.
={WriteLn}=
    ?[3]
    $(3)(G)
  {WriteNextChar}
    #(3$$)<EOStr>
    $(1)(3$$)
    $(3)(3$^1+)
    #<WriteNextChar>
  {EOStr}
    $(1)(13)
    $(1)(10)
**

;Процедура Write(<VarIndex>)
;Печатает на экране текстовую строку, начинающуюся
;с VarIndex, до первого нуля.
={Write}=
    ?[3]
    $(3)(G)
  {WriteNextChar2}
    #(3$$)<EOStr2>
    $(1)(3$$)
    $(3)(3$^1+)
    #<WriteNextChar2>
  {EOStr2}
**

;Функция FReadLn(<FileIndex>, <VarIndex>): <IsEOF>
;Читает из файла <FileIndex> текстовую строку, и
;помешает ее, начиная с переменной <VarIndex>
;IsEOF будет 0, если конец файла был достигнут, и 1
;в противном случае. Файл должен быть открыт.
={FReadLn}=
    ?[3,4,5]
    $(3)(G)
    $(4)(G)  
  {ReadNextByte}
    ~(12)(4$,3$,5)
    $(3)(3$^1+)
    #(5$)<EOL> ;Проверка на конец файла
    #(3$$^10-)<EOL> ;Проверка на конец строки
    #(3$$^13-)<EOL>
    #<ReadNextByte>
  {EOL}
    $(3$)(0)
    P(5$)
**

;Процедура FWriteLn(<FileIndex>, <VarIndex>)
;Записывает в файл <FileIndex> текстовую строку,
;с переменной <VarIndex>. Файл должен быть открыт.
={FWriteLn}=
    ?[3,4]
    $(3)(G)
    $(4)(G)  
  {WriteNextByte}
    ~(11)(4$,3$)
    $(3)(3$^1+)
    #(3$$)<EOL2> ;Проверка на конец строки
    #<WriteNextByte>
  {EOL2}
**