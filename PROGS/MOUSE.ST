;mouse.st. Запилил Абадябер =)
;Является примером, иллюстрирующим активную работу с подпрограммами, определениями define,
;и специальными функциями языка, такими, как работа с портами и вызов прерываний BIOS.
;Вообще, все это дело вполне можно запилить в библиотеку, ибо фактически это она самая и есть ;)

Define Regs='200' ;Регистры будут расположены с 200 переменной
Define MouseInfo='10'
Define Screen13='19' ;Экранный режим. 19 - 320x200x256; 18 - 640x480x16 и.т.п
Define TextMode='3'  ;Текстовой режим.
Define X='11'
Define Y='12'
Define C='13'

={InitMouse}=  ;Инициализирует мышь
    ?[3]
    $(3)(G)
    $(3$)(0) ;AH = 0
    $(3$^1+)(0) ;AL = 0
    ~(9)(51,3$)
    P(3$$)
**

={ShowMouse}=   ;Показывает курсор мыши
    ?[3]
    $(3)(G)
    $(3$)(0)
    $(3$^1+)(1) ;Al = 1
    ~(9)(51,3$)
**

={HideMouse}=   ;Прячет курсор мыши
    ?[3]
    $(3)(G)
    $(3$)(0)
    $(3$^1+)(2) ;Al = 2
    ~(9)(51,3$)
**

={CheckMouse}=    ;Получает информацию о состоянии мыши
    ?[3]
    $(3)(G)
    $(3$)(0)
    $(3$^1+)(3) ;Al = 1
    ~(9)(51,3$)
    P(3$^4+$^256*^3$^5+$+)    ;Push cx
    P(3$^6+$^256*^3$^7+$+)    ;Push dx
    P(3$^2+$^256*^3$^3+$+)    ;Push bx
**

={Scr}=   ;Устанавливает экранный режим
    ?[3,4]
    $(4)(G)
    $(3)(G)
    $(3$)(0)  ;AH = 0
    $(3$^1+)(4$)  ;AL = 4$
    ~(9)(16,3$) 
**

={PSet}=  ;Устанавливает точку по переданным координатам и цвету.
    ?[3,4,5,6]
    $(6)(G)
    $(5)(G)
    $(4)(G)
    $(3)(G)
    $(3$)(12)
    $(3$^1+)(6$)
    $(3$^2+)(0) 
    $(3$^7+)(4$^256%)   
    $(3$^6+)(4$^256/)
    $(3$^5+)(5$^256%)
    $(3$^4+)(5$^256/)
    ~(9)(16,3$)
**

Program
*[Regs,Screen13]<Scr>
*[Regs]<InitMouse>
$(MouseInfo)(G)
*[Regs]<ShowMouse>
$(X)(0)
$(Y)(0)
$(C)(0)
{Loop}
    *[Regs]<CheckMouse>
    $(C)(G)
    $(Y)(G)
    $(X)(G)
    #(C$|)<Next>
    *[Regs,Y$,X$^2/,X$^16%]<PSet>
#<Loop>
{Next}
$(X)(0)
$(Y)(0)
$(C)(0)
{Draw}
    *[Regs,Y$,X$,C$]<PSet>
    $(X)(X$^1+)
    #(X$^319-|)<Draw>
    $(X)(0)
    $(Y)(Y$^1+)
    $(C)(C$^1+)
#(Y$^199-|)<Draw>
*[Regs,TextMode]<Scr>